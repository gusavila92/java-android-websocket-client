plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'java-android-websocket-client'
        version {
	        name = '1.2.1'
	        gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
            }
    	}
    }
    publications = ['MyPublication']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            name "Gustavo Avila"
            email System.getenv('DEVELOPER_EMAIL')
        }
    }
    scm {
    	connection "scm:git:git://github.com/gusavila92/java-android-websocket-client.git"
    	developerConnection "scm:git:ssh://github.com:gusavila92/java-android-websocket-client.git"
    	url "http://github.com/gusavila92/java-android-websocket-client/tree/master"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'tech.gusavila92'
            artifactId 'java-android-websocket-client'
            version '1.2.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Java/Android WebSocket Client')
                root.appendNode('description', 'A very lightweight WebSocket client library for Java/Android which aims to implement the WebSocket protocol as defined in RFC 6455')
                root.appendNode('url', 'https://github.com/gusavila92/java-android-websocket-client')
                root.children().last() + pomConfig
            }
        }
    }
}
